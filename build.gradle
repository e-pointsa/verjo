buildscript {
    ext {
        repoConfig = {
            mavenCentral()

            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }

        jooqVersion = "3.10.4"
        junitVersion = "5.2.0"
    }

    repositories repoConfig

    dependencies {
        classpath "org.unbroken-dome.gradle-plugins:gradle-xjc-plugin:1.1.3"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.9.0"
    }
}

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "org.unbroken-dome.xjc"
apply plugin: "pl.allegro.tech.build.axion-release"
apply plugin: "jacoco"

repositories repoConfig

scmVersion {
    hooks {
        pre 'commit'
    }

    localOnly = true
}

group "pl.ppiorkowski"
version scmVersion.version
sourceCompatibility = 1.8

dependencies {
    compile "org.jooq:jooq-codegen:$jooqVersion"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1"

    compileOnly 'org.projectlombok:lombok:1.16.20'

    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.0.4"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-fluent-api:3.0"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-commons-lang:2.4"

    testCompile "com.google.jimfs:jimfs:1.1"
    testCompile "org.assertj:assertj-core:3.10.0"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "4.7"
    distributionType = Wrapper.DistributionType.ALL
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

xjcGenerate {
    source = fileTree('src/main/resources/schema/2_3') { include 'schema.xsd' }
    bindingFiles = fileTree('src/main/resources/schema/2_3/bindings') { include 'binding.xjb' }
    outputDirectory = file("gen/java")
    targetPackage = "pl.ppiorkowski.verjo.xsd"
    extraArgs = ['-Xannotate',
                 '-Xfluent-api',
                 '-XtoString',
                 '-Xequals',
                 '-XhashCode']
}

clean {
    delete "gen"
}